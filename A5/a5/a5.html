<html>
  <head>
    <meta charset="UTF-8">
    <title>CPSC 314 Assignment 5 Sep 2019</title>
    <style>
      body { margin: 0; } canvas {width: 100%;
      height: 100% }
    </style>
  </head>
  <body>
    <div id="canvas"></div>
<!--    <script src="js/three.js"></script> -->
    <script src="js/three.min.js"></script>
    <script src="js/OBJLoader.js"></script>
    <script src="js/SourceLoader.js"></script>
    <script src="js/OrbitControls.js"></script>
    <script src="js/KeyboardState.js"></script> <!-- By Jerome Etienne: http://jetienne.com/ -->

<script id="floorVertShader" type="x-shader/x-vertex">
uniform vec2 uvScale;
varying vec2 vUv;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main()
{       
  vUv = uv;
  ocsPosition = position;
  vec4 vcsP = modelViewMatrix * vec4(ocsPosition,1.0);
  vcsPosition = vcsP.xyz;
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vcsNormal = normalMatrix * normal;       // normalMatrix is the transpose-inv of the rotation matrix
}
</script>

<script id="myVertShader" type="x-shader/x-vertex">
uniform vec2 uvScale;
varying vec2 vUv;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main()
{       
  vUv = uvScale * uv;
  ocsPosition = position;
  vec4 vcsP = modelViewMatrix * vec4(ocsPosition,1.0);
  vcsPosition = vcsP.xyz;
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vcsNormal = normalMatrix * normal;       // normalMatrix is the transpose-inv of the rotation matrix
}
</script>

<script id="skyBoxVertShader" type="x-shader/x-vertex">
varying vec2 vUv;
void main()
{       
  vec4 vcsP = modelViewMatrix * vec4(position,1.0);
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vUv = uv;
}
</script>

<script id="holeyFragShader" type="x-shader/x-fragment">
// (d)
uniform vec3 lightPosition;
uniform vec3 myColor;
varying vec3 vcsNormal;
varying vec3 vcsPosition;
varying vec3 ocsPosition;

void main( void ) {
  vec3 L = normalize(lightPosition - vcsPosition);
  vec3 N = normalize(vcsNormal);
  float i = dot(N,L);
  float scaler = 4.0;

  float x = ocsPosition.x;
  float y = ocsPosition.y;
  float z = ocsPosition.z;
  float dx = x*scaler - floor(x*scaler+0.5);
  float dy = y*scaler - floor(y*scaler+0.5);
  float dz = z*scaler - floor(z*scaler+0.5);
  float r2 = dx*dx + dy*dy + dz*dz;
  if (r2 <= 0.15) {
    discard;
  } else {
    gl_FragColor = vec4(i*myColor, 1.0);
  }
  //vec3 color = vec3(0,1,1);
  //gl_FragColor = vec4(color,1.0);
}
</script>

<script id="toonFragShader" type="x-shader/x-fragment">
// (c)
varying vec3 vcsNormal;
varying vec3 vcsPosition;
uniform vec3 lightPosition;
uniform vec3 myColor;
void main( void ) {
  vec3 L = normalize(lightPosition - vcsPosition);
  vec3 N = normalize(vcsNormal);

  float i = dot(N,L);
  i = floor(4.0*i) / 4.0;
  //gl_FragColor = vec4(0.9,0.9,0.3,1.0);   // default is yellow
  gl_FragColor = vec4(myColor*i,1.0);
}
</script>




<script id="envmapFragShader" type="x-shader/x-fragment">
// (f) (g)
uniform vec3 lightPosition;
uniform sampler2D uPosxTexture;
uniform sampler2D uPosyTexture;
uniform sampler2D uPoszTexture;
uniform sampler2D uNegxTexture;
uniform sampler2D uNegyTexture;
uniform sampler2D uNegzTexture;
uniform vec3 myColor;
uniform mat4 matrixWorld;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main( void ) {
  vec3 I = normalize(vcsPosition);
  vec3 N = normalize(vcsNormal);
  vec3 R = reflect(I,N);
  vec4 newvec = vec4(R,1.0)*matrixWorld;

  if (newvec.y >= 0.0 && newvec.y >= abs(newvec.x) && newvec.y >= abs(newvec.z)) {
    float u = ((-newvec.x / newvec.y) + 1.0) / 2.0;
    float v = ((-newvec.z / newvec.y) + 1.0) / 2.0;
    vec3 color = texture2D(uPosyTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color, 1.0);
  }
  if (newvec.y < 0.0 && abs(newvec.y) >= abs(newvec.x) && abs(newvec.y) >= abs(newvec.z)) {
    float u = ((-newvec.x / newvec.y) + 1.0) / 2.0;
    float v = ((newvec.z / newvec.y) + 1.0) / 2.0;
    vec3 color = texture2D(uNegyTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color, 1.0);
  }
  if (newvec.x >= 0.0 && newvec.x >= abs(newvec.y) && newvec.x >= abs(newvec.z)) {
    float u = ((newvec.z / newvec.x) + 1.0) / 2.0;
    float v = ((newvec.y / newvec.x) + 1.0) / 2.0;
    vec3 color = texture2D(uPosxTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color, 1.0);
  }
  if (newvec.x < 0.0 && abs(newvec.x) >= abs(newvec.y) && abs(newvec.x) >= abs(newvec.z)) {
    float u = ((newvec.z / newvec.x) + 1.0) / 2.0;
    float v = ((-newvec.y / newvec.x) + 1.0) / 2.0;
    vec3 color = texture2D(uNegxTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color, 1.0);
  }
  if (newvec.z >= 0.0 && newvec.z >= abs(newvec.x) && newvec.z >= abs(newvec.y)) {
    float u = ((-newvec.x / newvec.z) + 1.0) / 2.0;
    float v = ((newvec.y / newvec.z) + 1.0) / 2.0;
    vec3 color = texture2D(uPoszTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color, 1.0);
  }
  if (newvec.z < 0.0 && abs(newvec.z) >= abs(newvec.x) && abs(newvec.z) >= abs(newvec.y)) {
    float u = ((-newvec.x / newvec.z) + 1.0) / 2.0;
    float v = ((-newvec.y / newvec.z) + 1.0) / 2.0;
    vec3 color = texture2D(uNegzTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color, 1.0);
  }

  //vec3 N = normalize(vcsNormal);
  //gl_FragColor = vec4(N,1.0);
}
</script>

<script id="floorFragShader" type="x-shader/x-fragment">
// (e)
uniform vec3 lightPosition;
uniform vec3 myColor;
varying vec3 vcsPosition;
varying vec3 ocsPosition;
uniform sampler2D normalMap;
uniform sampler2D textureMap;
varying vec2 vUv;

void main( void ) {
  vec3 color = texture2D(textureMap, vUv).xyz;
  vec3 normalColor = texture2D(normalMap, vUv).xyz;

  float Nx = 2.0*normalColor.x - 1.0;
  float Ny = 2.0*normalColor.y - 1.0;
  float Nz = 2.0*normalColor.z - 1.0;

  vec3 temp = vec3(Nx, Ny, Nz);
  vec3 N = normalize(temp);
  mat3 rotation = mat3(1.0,0.0,0.0,
                       0.0,0.0,-1.0,
                       0.0,1.0,0.0);
  N = rotation*N;
  vec3 L = normalize(lightPosition - vcsPosition);
  float i = dot(N,L);


  //gl_FragColor = vec4(normalColor,1.0);
  gl_FragColor = vec4(i*color,1.0);
}
</script>


<script id="pnoiseFragShader" type="x-shader/x-fragment">
float screenWidth = 2.0;
float PI = 3.1416;

// perlin noise code from: 
//   https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83

float rand(vec2 c){
	return fract(sin(dot(c.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

float noise(vec2 p, float freq ){
	float unit = screenWidth/freq;
	vec2 ij = floor(p/unit);
	vec2 xy = mod(p,unit)/unit;
	//xy = 3.*xy*xy-2.*xy*xy*xy;
	xy = .5*(1.-cos(PI*xy));
	float a = rand((ij+vec2(0.,0.)));
	float b = rand((ij+vec2(1.,0.)));
	float c = rand((ij+vec2(0.,1.)));
	float d = rand((ij+vec2(1.,1.)));
	float x1 = mix(a, b, xy.x);
	float x2 = mix(c, d, xy.x);
	return mix(x1, x2, xy.y);
}

float pNoise(vec2 p, int res){
	float persistance = .5;
	float n = 0.;
	float normK = 0.;
	float f = 4.;
	float amp = 1.;
	int iCount = 0;
	for (int i = 0; i<50; i++){
		n+=amp*noise(p, f);
		f*=2.;
		normK+=amp;
		amp*=persistance;
		if (iCount == res) break;
		iCount++;
	}
	float nf = n/normK;
	return nf*nf*nf*nf;
}

varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main( void ) {
  int levels = 2;
  // int levels = 5;
  float i=pNoise(vcsPosition.xy,levels);
  // float i=pNoise(ocsPosition.xy,levels);
  float j = floor(i + 0.9);
  gl_FragColor = vec4(j,j,0.3,1.0);
  // gl_FragColor = vec4(i,i,i,1.0);
}
/* 1. changed to ocsPosition: the noise is static when the scene is rotated (when using vcsPosition,
      the noise will change as we rotate the scene
   2. increasing the level: the boundary of yellow part is less smooth than using smaller level */
</script>
    <script id="myFragShader" type="x-shader/x-fragment">
      varying vec3 vcsNormal;
      varying vec3 vcsPosition;
      varying vec3 ocsPosition;
      uniform vec3 lightPosition;
      uniform vec3 myColor;
      void main( void ) {
        vec3 L = normalize(lightPosition - vcsPosition);
        vec3 N = normalize(vcsNormal);

        float i = dot(N,L);

        //gl_FragColor = vec4(0.9,0.9,0.3,1.0);   // default is yellow
        if (ocsPosition.x * ocsPosition.x + ocsPosition.z * ocsPosition.z >= 1.0){
        gl_FragColor = vec4(myColor*i,1.0);} else {

        gl_FragColor = i*vec4(0.9,0.9,0.3,1.0);}
      }
    </script>

<script src="a5.js"></script>
</body>
</html>

